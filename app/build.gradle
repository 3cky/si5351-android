apply plugin: 'com.android.application'
apply plugin: 'maven-publish'

android {
    compileSdkVersion versions.sdk.compile

    defaultConfig {
        applicationId "com.github.ykc3.android.si5351.app"

        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target

        versionCode versions.project.major * 1000000 + versions.project.minor * 1000 + versions.project.patch
        versionName versions.project.number
    }

    // Release management
    signingConfigs {
        release {
            File propsFile = project.rootProject.file('local.properties')
            if (propsFile.exists()) {
                Properties properties = new Properties()
                properties.load(propsFile.newDataInputStream())
                keyAlias properties.getProperty('key.alias')
                keyPassword properties.getProperty('key.password')
                storeFile file(properties.getOrDefault('key.store.file', 'release.keystore'))
                storePassword properties.getProperty('key.store.password')
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

afterEvaluate {
    publishing {
        publications {
            releaseApk(MavenPublication) {
                from components.release_apk

                groupId 'com.github.3cky'
                artifactId 'si5351-android-app'
                version versions.project.number

                artifact source: file('build/outputs/apk/release/app-release.apk'),
                        classifier: 'signed', extension: 'apk'
            }
            releaseAab(MavenPublication) {
                from components.release_aab

                groupId 'com.github.3cky'
                artifactId 'si5351-android-app'
                version versions.project.number
            }
        }
    }
}

dependencies {
    implementation project(':lib')

    implementation 'com.github.3cky:DecimalNumberPicker:1.0.0'

    implementation 'com.google.android.material:material:1.6.1'
    implementation "androidx.gridlayout:gridlayout:1.0.0"
}